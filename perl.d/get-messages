#!/usr/bin/perl -w
#
#  This script gets ALL the messages from the given folder, and dumps them
# as a JSON array of hashes.
#
#  It is invoked from src/global_state.cc.
#
#

use strict;
use warnings;

use Cwd 'abs_path';
use File::Basename;


use lib File::Basename::dirname( abs_path($0) );
use Lumail;

use JSON;


#
#  Arguments from the command-line.
#
my $folder = shift;
if ( !$folder )
{
    print "Invalid command-line arguments\n";
    exit(0);
}


my $handle = Lumail::imap_connect();

# If that failed show the user and abort.
if ( !$handle )
{
    print "$0 - Login failed.\n";
    exit(0);
}

#
#  Count the messages we need to fetch
#
my $status = $handle->status($folder);
my $total  = $status->{ MESSAGES };

#
# Select the folder
#
$handle->select($folder) or die "Failed to select folder: $folder";

#
#  Perform a search - to ge the message IDs of all the messages
# in the given folder.
#
my $all = $handle->search("all");

my @ids;
foreach my $a (@$all)
{
    push @ids, $a;
}


#
#  Perform the retrival.  We wish to retrieve both:
#
#   1.  The flag(s) of each message.
#
#   2.  The body-text of each message.
#
my $results = $handle->fetch( \@ids, "FLAGS BODY.PEEK[]" ) or die "fail";

my $tmp;

if ( ($results) && ( ref( \$results ) eq "REF" ) )
{

    foreach my $hash (@$results)
    {
        my @flags;
        foreach my $x ( @{ $hash->{ 'FLAGS' } } )
        {
            push( @flags, $x );
        }
        my $flags = join( ",", @flags );
        push( @$tmp,
              {  id    => $hash->{ 'UID' },
                 msg   => $hash->{ 'BODY[]' },
                 flags => $flags
              } );
    }

}

my %hash;
$hash{ 'messages' } = $tmp;

my $t = JSON->new->allow_nonref;
my $o = $t->pretty->encode( \%hash );
print $o;
