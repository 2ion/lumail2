#!/usr/bin/perl -w
#
#  This script saves the specified filename to the remote IMAP server.
#

use strict;
use warnings;

use Net::IMAP::Client;


#
#  Arguments from the command-line.
#
my $file = shift;

#
# Arguments from the environment.
#
my $user = $ENV{ 'imap_username' };
my $pass = $ENV{ 'imap_password' };
my $imap = $ENV{ 'imap_server' };

#
#  Get the values we're going to connect to the IMAP server with.
#
my $port = 0;
my $ssl  = 0;
my $host = "";
if ( $imap =~ /^imaps:\/\/([^\/]+)\/?$/i )
{
    $port = 993;
    $host = $1;
    $ssl  = 1;
}
elsif ( $imap =~ /^imap:\/\/([^\/]+)\/?$/i )
{
    $port = 143;
    $host = $1;
    $ssl  = 0;
}


#
#  Create the helper
#
my $handle = Net::IMAP::Client->new(
    server          => $host,
    user            => $user,
    pass            => $pass,
    ssl             => $ssl,
    ssl_verify_peer => 0,
    port            => $port

  ) or
  die "Could not connect to IMAP server";


$handle->login or die( 'Login failed: ' . $handle->last_error() );

#
#  Get the list of folders, and their flags.
#
my $data = $handle->folders_more();

#
#  For each folder we've found look at the flags, and
# see if we can find one with `\Sent`, which is where we'll
# save the message.
#
my $sent = "";

foreach my $folder (%$data)
{
    my $entry = $data->{ $folder };

    my $flags = $entry->{ 'flags' };

    foreach my $flag (@$flags)
    {
        $sent = $folder if ( $flag =~ /\Sent/i );
    }
}

#
#  Did we find one?
#
if ($sent)
{
    my $msg = "";
    open( my $h, "<", $file ) or
      die "Failed to read $file - $!";
    while ( my $line = <$h> )
    {
        $msg .= $line;
    }
    close($h);
    $handle->append( $sent, \$msg );
}
else
{
    print "Failed to find folder to save message\n";
}

$handle->quit();
exit(0);
