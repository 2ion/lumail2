#!/usr/bin/perl -w
#
#  This script gets ALL the messages from the given folder.
#
#  For example to get the first message run:
#
##    ./get-messages  steve-org-uk
#
# Where "steve-org-uk" is the name of the folder.
#
#

use strict;
use warnings;

use JSON;
use Net::IMAP::Client;



#
#  Arguments from the command-line.
#
my $folder = shift;

#
# Arguments from the environment.
#
my $user = $ENV{ 'imap_username' };
my $pass = $ENV{ 'imap_password' };
my $imap = $ENV{ 'imap_server' };

#
#  Get the values we're going to connect to the IMAP server with.
#
my $port = 0;
my $ssl  = 0;
my $host = "";
if ( $imap =~ /^imaps:\/\/([^\/]+)\/?$/i )
{
    $port = 993;
    $host = $1;
    $ssl  = 1;
}
elsif ( $imap =~ /^imap:\/\/([^\/]+)\/?$/i )
{
    $port = 143;
    $host = $1;
    $ssl  = 0;
}


#
#  Create the helper
#
my $handle = Net::IMAP::Client->new(
    server          => $host,
    user            => $user,
    pass            => $pass,
    ssl             => $ssl,
    ssl_verify_peer => 0,
    port            => $port

  ) or
  die "Could not connect to IMAP server";


$handle->login or die( 'Login failed: ' . $handle->last_error() );

#
#  Count the messages we need to fetch
#
my $status = $handle->status($folder);
my $total  = $status->{ MESSAGES };

#
# Select the folder
#
$handle->select($folder) or die "Failed to select folder: $folder";

#
#  Show a search
#
my $all = $handle->search("all");

my @ids;
foreach my $a (@$all)
{
    push @ids, $a;
}
#
#  Get the stuff
#
my $results = $handle->fetch( \@ids, "FLAGS BODY.PEEK[]" ) or die "fail";

my $tmp;

foreach my $hash (@$results)
{
    my @flags;
    foreach my $x ( @{ $hash->{ 'FLAGS' } } )
    {
        push( @flags, $x );
    }
    my $flags = join( ",", @flags );
    push( @$tmp,
          {  id    => $hash->{ 'UID' },
             msg   => $hash->{ 'BODY[]' },
             flags => $flags
          } );
}

my %hash;
$hash{ 'messages' } = $tmp;

my $t = JSON->new->allow_nonref;
my $o = $t->pretty->encode( \%hash );
print $o;
